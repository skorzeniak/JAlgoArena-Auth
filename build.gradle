buildscript {
    ext.kotlin_version = '1.0.6'
    ext.spring_version = '1.4.0.RELEASE'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

plugins {
    id "jacoco"
}

apply plugin: 'kotlin'
apply plugin: 'spring-boot'
apply plugin: 'application'
apply plugin: 'docker'

apply from: "$rootDir/gradle/versioning.gradle"

group = 'jalgoarena'
description = """JAlgoArena Auth Server"""

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jar {
    baseName = 'jalgoarena-auth'
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind',
            'com.fasterxml.jackson.core:jackson-core',
            'com.fasterxml.jackson.core:jackson-annotations',
            "com.fasterxml.jackson.module:jackson-module-kotlin",
            "org.springframework.boot:spring-boot-starter-web",
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.boot:spring-boot-starter-security",
            'org.springframework.cloud:spring-cloud-starter-eureka',
            'org.springframework.security:spring-security-config',
            "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version",
            "io.jsonwebtoken:jjwt:0.7.0",
            'org.jetbrains.xodus:xodus-openAPI:1.0.2',
            'org.jetbrains.xodus:xodus-entity-store:1.0.2',
            'io.springfox:springfox-swagger2:2.7.0',
            'io.springfox:springfox-swagger-ui:2.7.0'

    runtime 'ch.qos.logback:logback-classic:1.1.7',
            'ch.qos.logback:logback-core:1.1.7'

    testCompile 'junit:junit:4.12',
            "org.springframework.boot:spring-boot-starter-test"
}

apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

defaultTasks 'clean', 'bootRepackage'

task stage {
    dependsOn 'bootRepackage'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Camden.SR3"
    }
}

task releaseZip(type: Zip, dependsOn: 'bootRepackage') {
    from('build/libs/') {
        include('*.jar')
    }
    from 'run.sh'
    archiveName "JAlgoArena-Auth-${version}.zip"
    destinationDir(file('build/libs'))
}

docker {
    baseImage "java:8"
}
